// Tested on an online interpreter: http://tetsuo.jp/janus-playground-v1.1/

// スタックを用いた通常型
procedure search_stack_landauer(stack s, int key, int f, stack g)
  local int x = 0
    push(1, g)
    from top(g) = 1 loop
      if top(s) = key then
        push(f, g)
        f ^= 1
        push(1, g)
      else
        push(0, g)
      fi top(g) = 1
      push(x, g)
      pop(x, s)
      push(0, g)
    until f != 0 || empty(s)    
  
    push(x, g)
  delocal int x = 0

procedure search_stack_bennett(stack s, int key, int f)
  local stack g = nil, int ff = 0
    call search_stack_landauer(s, key, ff, g)
    f ^= ff
    uncall search_stack_landauer(s, key, ff, g)
  delocal stack g = nil, int ff = 0

procedure main()
  stack s = {1,2,3,4,5,6,7,8,9,10}
  int k = 51
  int f
   
  call search_stack_bennett(s,k,f)
  printf("The result is %d", f)
  k ^= 51 // zero clear

  k ^= 6
  call search_stack_bennett(s,k,f)
  printf("The result is %d", f)
